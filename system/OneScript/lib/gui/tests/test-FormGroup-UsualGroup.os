#Использовать asserts
#Использовать gui

Перем ВидыПоляФормы;
Перем Форма;
Перем ТекстПроверки;

Функция ПолучитьСписокТестов(Тестирование) Экспорт

	СписокТестов = Новый Массив;
	СписокТестов.Добавить("Тест_Должен_СоздатьПоле");
	СписокТестов.Добавить("Тест_Должен_ПеребратьПодчиненныеЭлементы");
	СписокТестов.Добавить("Тест_Должен_ОбработатьПеремещение");
	
	Возврат СписокТестов;

КонецФункции

//# Работа с событиями
Процедура ПриОткрытииФормы() Экспорт
	Форма.Закрыть();
КонецПроцедуры

Процедура ПолучитьФорму()

	УправляемыйИнтерфейс = Новый УправляемыйИнтерфейс();
	Форма = УправляемыйИнтерфейс.СоздатьФорму();
	Форма.УстановитьДействие(ЭтотОбъект, "ПриОткрытии", "ПриОткрытииФормы");
	ВидыПоляФормы = Форма.ВидГруппыФормы;

КонецПроцедуры

Процедура Тест_Должен_СоздатьПоле() Экспорт

	ПолучитьФорму();

	Поле1 = Форма.Элементы.Добавить("Поле1", "ГруппаФормы", Неопределено);
	Поле1.Вид = ВидыПоляФормы.ОбычнаяГруппа;
	Форма.Показать();

	Ожидаем.Что(Форма.Элементы.Найти("Поле1")).Существует();

КонецПроцедуры


Процедура Тест_Должен_ПеребратьПодчиненныеЭлементы() Экспорт

	ПолучитьФорму();

	Группа1 = Форма.Элементы.Добавить("Группа1", "ГруппаФормы", Неопределено);
	Группа1.Вид = ВидыПоляФормы.ОбычнаяГруппа;

	Элемент1 = Форма.Элементы.Добавить("Поле1", "ПолеФормы", Группа1);
	Элемент1.Вид = Форма.ВидПоляФормы.ПолеВвода;

	Элемент1 = Форма.Элементы.Добавить("Поле2", "ПолеФормы", Группа1);
	Элемент1.Вид = Форма.ВидПоляФормы.ПолеВвода;

	СтрГрп1 = "";
	Для каждого Элемент Из Группа1.Элементы Цикл
		СтрГрп1 = СтрГрп1 + "," + Элемент.Имя;
	КонецЦикла;	

	СтрФрм = "";
	Для каждого Элемент Из Форма.Элементы Цикл
		СтрФрм = СтрФрм + "," + Элемент.Имя;
	КонецЦикла;	
	Форма.Показать();

	Ожидаем.Что(Сред(СтрГрп1, 2)).Равно("Поле1,Поле2");
	Ожидаем.Что(Сред(СтрФрм, 2)).Равно("Группа1");
	
КонецПроцедуры

Процедура Тест_Должен_ОбработатьПеремещение() Экспорт

	ПолучитьФорму();

	Группа1 = Форма.Элементы.Добавить("Группа1", "ГруппаФормы", Неопределено);
	Группа1.Вид = ВидыПоляФормы.ОбычнаяГруппа;

	Группа2 = Форма.Элементы.Добавить("Группа2", "ГруппаФормы", Неопределено);
	Группа2.Вид = ВидыПоляФормы.ОбычнаяГруппа;

	Элемент1 = Форма.Элементы.Добавить("Поле1", "ПолеФормы", Группа1);
	Элемент1.Вид = Форма.ВидПоляФормы.ПолеВвода;

	Элемент1 = Форма.Элементы.Добавить("Поле2", "ПолеФормы", Группа1);
	Элемент1.Вид = Форма.ВидПоляФормы.ПолеВвода;

	Форма.Элементы.Переместить(Элемент1, Группа2, Неопределено);
	Форма.Показать();

	Ожидаем.Что(Группа2.Элементы.Количество()).Равно(1);
	Ожидаем.Что(Группа1.Элементы.Количество()).Равно(1);
	Ожидаем.Что(Форма.Элементы.Количество()).Равно(2);

КонецПроцедуры
