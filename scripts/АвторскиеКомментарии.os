// #Использовать logos

Перем Обмен;
Перем Настройки;
Перем Окружение;

Функция ПолучитьСигантуру(Стр)

	Если Настройки.Автор = "%ПользовательОС%" Тогда
		СтрПользователь = Окружение.ПеременныеСреды()["USERNAME"];
	Иначе
		СтрПользователь = Настройки.Автор;
	КонецЕсли;

	Если Настройки.Компания = "%ДоменОС%" Тогда
		СтрКомпания = Окружение.ПеременныеСреды()["USERDNSDOMAIN"];
	Иначе
		СтрКомпания = Настройки.Компания;
	КонецЕсли;

	СтрДата = Формат(ТекущаяДата(), Настройки.ФорматДаты);

	Стр = СтрЗаменить(Стр,"%Author%", СтрПользователь);
	Стр = СтрЗаменить(Стр,"%Company%", СтрКомпания);
	Стр = СтрЗаменить(Стр,"%Date%", СтрДата);

	Возврат Стр;
КонецФункции

Функция ЗакоментироватьБлок(Данные)
	Код = СокрП(Данные);
	РегВыражение = Новый РегулярноеВыражение("^");
	Код = РегВыражение.Заменить(Код,"//");
	возврат Код;
КонецФункции // ЗакоментироватьБлок()

Процедура ОбработкаТекста(Данные,Приемник,Действие)
	НовыеДанные = "";
	Если Действие = "add" Тогда
		НовыеДанные = "//" + Настройки["МеткаДобавлено"] + " " + ПолучитьСигантуру(Настройки.СигнатураНачало)
			+ Символы.ПС
			+ СокрЛП(Данные)
			+ Символы.ПС
			+ "//" + ПолучитьСигантуру(Настройки.СигнатураКонец);
	ИначеЕсли Действие = "edit" Тогда
		НовыеДанные = "//" + Настройки["МеткаИзменено"] + " " + ПолучитьСигантуру(Настройки.СигнатураНачало)
			+ Символы.ПС;
		Если Настройки.СохранятьКодПриИзменении Тогда
			НовыеДанные = НовыеДанные
				+ ЗакоментироватьБлок(СокрЛП(Данные))
				+ Символы.ПС
		КонецЕсли;
		НовыеДанные = НовыеДанные
			+ СокрЛП(Данные)
			+ Символы.ПС
			+ "//" + ПолучитьСигантуру(Настройки.СигнатураКонец);
	ИначеЕсли Действие = "del" Тогда
		НовыеДанные = "//" + Настройки["МеткаУдалено"] + " " + ПолучитьСигантуру(Настройки.СигнатураНачало)
			+ Символы.ПС
			+ ЗакоментироватьБлок(СокрЛП(Данные))
			+ Символы.ПС
			+ "//" + ПолучитьСигантуру(Настройки.СигнатураКонец);
	КонецЕсли;	

	НовыеДанные = НовыеДанные  + Символы.ПС;
	Обмен.ЗаписатьРезультатВФайл(Приемник,НовыеДанные);

КонецПроцедуры

Процедура Выполнить(Параметры)
	
	Действие = Параметры[0];

	ИмяФайла = "tmp\module.txt";
	Если Параметры.Количество() > 1 Тогда
		ИмяФайла = Параметры[1];
	КонецЕсли;
	Приемник = ИмяФайла;

	Если Параметры.Количество() > 2 Тогда
		Приемник = Параметры[2];
	КонецЕсли;

	ОбработкаТекста(Обмен.ПолучитьТекстИзФайла(ИмяФайла),Приемник,Действие);


КонецПроцедуры


Обмен = ЗагрузитьСценарий("scripts\Обмен.os");
Настройки = ЗагрузитьСценарий("settings\ПараметрыАвторскиеКомментарии.os").Настройки;
Окружение = Новый СистемнаяИнформация();

Выполнить(АргументыКоманднойСтроки);


//МассивПарамеров = новый Массив;
//МассивПарамеров.Добавить("Добавлено");
//МассивПарамеров.Добавить("c:\work\portable\v8CfgAddsAhk\tmp\module.txt");
//МассивПарамеров.Добавить("c:\work\portable\v8CfgAddsAhk\tmp\new.module.txt");

//Выполнить(МассивПарамеров);